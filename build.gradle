plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id "maven"
    id "maven-publish"
    id "java-library"
    id "com.jfrog.bintray" version "1.8.4"
}

group "com.luzon"
version "$version"

sourceCompatibility = targetCompatibility = 1.8

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "io.github.microutils:kotlin-logging:1.6.25"

    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spekVersion"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spekVersion"
    
    testImplementation "org.amshove.kluent:kluent:1.47"

    testImplementation "com.squareup.okio:okio:$okioVersion"
    testImplementation "com.squareup.moshi:moshi:1.8.0"
}

repositories {
    mavenCentral()
    jcenter()
}

test {
    useJUnitPlatform {
        includeEngines "spek2"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

bintray {
    user = project.hasProperty("bintrayUser") ? project.property("bintrayUser") : System.getenv("BINTRAY_USER")
    key = project.hasProperty("bintrayApiKey") ? project.property("bintrayApiKey") : System.getenv("BINTRAY_API_KEY")
    configurations = ["archives"]

    pkg {
        repo = "luzon"
        name = "luzon"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/ch629/luzon.git"

        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "ch629"
            name "ch629"
            email "charliehowes97@outlook.com"
        }
    }

    scm {
        url "https://github.com/ch629/luzon"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
//            artifact javadocJar
            groupId "com.luzon"
            artifactId "luzon"
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode("description", "Embedded Customizable Language.")
                root.appendNode("name", "luzon")
                root.appendNode("url", "https://www.github.com/ch629/luzon")
                root.children().last() + pomConfig
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

artifacts {
    archives sourcesJar
    archives jar
}
