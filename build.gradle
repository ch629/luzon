buildscript {
    repositories {
        mavenCentral()
        jcenter()

        maven {
            name "kotlin-eap"
            url "https://dl.bintray.com/kotlin/kotlin-eap/"
        }
    }

    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id "maven"
    id "maven-publish"
    id "java-library"
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: "org.junit.platform.gradle.plugin"

group 'com.luzon'
version '1.0.1'

sourceCompatibility = targetCompatibility = 1.8

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    implementation "com.squareup.okio:okio:$okioVersion"
    implementation "io.arrow-kt:arrow-core:$arrowVersion"

    implementation "ch.qos.logback:logback-classic:1.3.0-alpha4"
    implementation "io.github.microutils:kotlin-logging:1.5.9"

    testImplementation "junit:junit:4.12"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testRuntime "org.junit.vintage:junit-vintage-engine:$junitVersion"

    testImplementation "org.jetbrains.spek:spek-api:$spekVersion"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spekVersion"

    testImplementation "com.squareup.okio:okio:2.2.2"
    testImplementation "com.squareup.moshi:moshi:1.8.0"

    testImplementation "org.amshove.kluent:kluent:1.38"
}

repositories {
    mavenCentral()
    jcenter()

    maven {
        name "kotlin-eap"
        url "https://dl.bintray.com/kotlin/kotlin-eap/"
    }
}

junitPlatform {
    filters {
        engines {
            include "spek"
        }
    }
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")
    configurations = ["archives"]

    pkg {
        repo = "luzon"
        name = "luzon-main"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/ch629/luzon.git"

        version {
            name = version
            released = new Date()
            vcsTag = version
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "ch629"
            name "ch629"
            email "charliehowes97@outlook.com"
        }
    }

    scm {
        url "https://github.com/ch629/luzon"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
//            artifact javadocJar
            groupId "com.luzon"
            artifactId "luzon"
            version version

            pom.withXml {
                def root = asNode()
                root.appendNode("description", "Embedded Customizable Language.")
                root.appendNode("name", "luzon")
                root.appendNode("url", "https://www.github.com/ch629/luzon")
                root.children().last() + pomConfig
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

artifacts {
    archives sourcesJar
    archives jar
}
