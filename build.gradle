plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.30"
    id "maven"
    id "maven-publish"
    id "java-library"
    id "com.jfrog.bintray" version "1.8.4"
    id "com.diffplug.gradle.spotless" version "3.28.1"
    id "jacoco"
}

group "com.luzon"
version "$version"

sourceCompatibility = targetCompatibility = 1.8

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    implementation "ch.qos.logback:logback-classic:$logbackVersion"
    implementation "io.github.microutils:kotlin-logging:$loggingVersion"

    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spekVersion"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spekVersion"

    testImplementation "org.amshove.kluent:kluent:$kluentVersion"

    testImplementation "com.squareup.okio:okio:$okioVersion"
    testImplementation "com.squareup.moshi:moshi:$moshiVersion"
}

repositories {
    mavenCentral()
    jcenter()
}

test {
    useJUnitPlatform {
        includeEngines "spek2"
    }

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }

    finalizedBy jacocoTestReport
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

bintray {
    user = project.hasProperty("bintrayUser") ? project.property("bintrayUser") : System.getenv("BINTRAY_USER")
    key = project.hasProperty("bintrayApiKey") ? project.property("bintrayApiKey") : System.getenv("BINTRAY_API_KEY")
    configurations = ["archives"]

    pkg {
        repo = "luzon"
        name = "luzon"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/ch629/luzon.git"

        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "ch629"
            name "ch629"
            email "charliehowes97@outlook.com"
        }
    }

    scm {
        url "https://github.com/ch629/luzon"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourceJar
//            artifact javadocJar
            groupId "com.luzon"
            artifactId "luzon"
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode("description", "Embedded Customizable Language.")
                root.appendNode("name", "luzon")
                root.appendNode("url", "https://www.github.com/ch629/luzon")
                root.children().last() + pomConfig
            }
        }
    }
}

artifacts {
    archives sourceJar
    archives jar
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    dependsOn test

    reports {
        csv.enabled false
        html.enabled false
        xml.enabled true
    }
}

spotless {
    format "misc", {
        target "**/*.gradle", "**/*.md", "**/.gitignore"

        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }

    kotlin {
        ktlint().userData(['insert_final_newline': 'true'])
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }

    java {
        googleJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
}
