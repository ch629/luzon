buildscript {
    repositories {
        mavenCentral()
        jcenter()

        maven {
            name "kotlin-eap"
            url "https://dl.bintray.com/kotlin/kotlin-eap/"
        }
    }

    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "maven"
}

subprojects {
    apply plugin: "java"
    apply plugin: "kotlin"
    apply plugin: "org.junit.platform.gradle.plugin"

    group 'com.luzon'
    version '1.0-SNAPSHOT'

    sourceCompatibility = targetCompatibility = 1.8

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

        compile "com.squareup.okio:okio:$okioVersion"
        compile "io.arrow-kt:arrow-core:$arrowVersion"

        compile "ch.qos.logback:logback-classic:1.3.0-alpha4"
        compile "io.github.microutils:kotlin-logging:1.5.9"

        testCompile "junit:junit:4.12"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        testRuntime "org.junit.vintage:junit-vintage-engine:$junitVersion"

        testCompile "org.jetbrains.spek:spek-api:$spekVersion"
        testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spekVersion"

        testCompile "org.amshove.kluent:kluent:1.38"
    }

    repositories {
        mavenCentral()
        jcenter()

        maven {
            name "kotlin-eap"
            url "https://dl.bintray.com/kotlin/kotlin-eap/"
        }
    }

    junitPlatform {
        filters {
            engines {
                include "spek"
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = "sources"
    }

    artifacts {
        archives sourcesJar
        archives jar
    }
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path)}

task allJar(type: Jar, dependsOn: subprojects.assemble) {
    baseName = 'luzon-all'
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
}

artifacts {
    archives allJar
//    archives sourcesJar
}